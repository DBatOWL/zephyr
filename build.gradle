plugins {
    id 'pmd'
    id 'maven-publish'
    id "com.diffplug.gradle.spotless" version "3.26.0"
    id "com.github.spotbugs" version "2.0.0"
    id "org.springframework.boot" version "2.2.3.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.gradle.spotless"

    dependencyManagement {
        imports {
            mavenBom "io.zephyr:bom-imported:$project.version"
        }
    }


    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username "$mavenRepositoryUsername"
                    password "$mavenRepositoryPassword"
                }
                url "$mavenRepositoryUrl"
            }
        }
    }

}

subprojects {


    plugins.withType(WarPlugin) {


        configurations {
            plugin {
                transitive = false
                extendsFrom implementation
            }
        }


        tasks.withType(com.github.spotbugs.SpotBugsTask) {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }
        pmd {
            ruleSets = [
                    "${project.rootDir}/check/pmd/rules/errorprone.xml",
                    "category/java/multithreading.xml",
                    "category/java/bestpractices.xml",
                    "category/java/performance.xml"
            ]

        }

        publishing {
            publications {
                webArchive(MavenPublication) {
                    from components.web
                }
            }
        }
    }



    plugins.withType(JavaPlugin) {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
        tasks.withType(com.github.spotbugs.SpotBugsTask) {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }
        pmd {
            ruleSets = [
                    "${project.rootDir}/check/pmd/rules/errorprone.xml",
                    "category/java/multithreading.xml",
                    "category/java/bestpractices.xml",
                    "category/java/performance.xml"
            ]

        }

        test {
            useJUnitPlatform()
        }
        spotless {
            java {
                googleJavaFormat('1.1')
            }
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            testAnnotationProcessor 'org.projectlombok:lombok'
        }
    }

}
