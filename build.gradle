plugins {
    id 'pmd'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.8.2"
    id "com.github.spotbugs" version "4.6.0"
    id "org.springframework.boot" version "2.2.4.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

apply from: "$rootDir/tasks/revisions.gradle"

allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: "com.diffplug.spotless"
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "io.zephyr:bom-imported:$project.version"
        }
    }


    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            url "https://repository.mulesoft.org/nexus/content/repositories/public/"
        }
    }

//    publishing {
//        repositories {
//            mavenLocal()
//            maven {
//                credentials {
//                    username "$mavenRepositoryUsername"
//                    password "$mavenRepositoryPassword"
//                }
//                url "$mavenRepositoryUrl"
//            }
//        }
//    }

}

subprojects {


    plugins.withType(WarPlugin) {


        configurations {
            plugin {
                transitive = false
                extendsFrom implementation
            }
        }


//        tasks.withType(com.github.spotbugs.SpotBugsTask) {
//            reports {
//                xml.enabled = false
//                html.enabled = true
//            }
//        }
        pmd {
            toolVersion = '6.30.0'
            ruleSets = [
                    "${project.rootDir}/check/pmd/rules/errorprone.xml",
                    "category/java/multithreading.xml",
                    "category/java/bestpractices.xml",
                    "category/java/performance.xml"
            ]

        }

        publishing {
            publications {
                webArchive(MavenPublication) {
                    from components.web
                }
            }
        }
    }


    plugins.withType(JavaPlugin) {


        task packageJavadocs(type: Jar, dependsOn: 'javadoc') {
            classifier 'javadoc'
            version project.version
            from javadoc.destinationDir
        }

        javadoc {
            source = sourceSets.main.allJava
            classpath += configurations.runtimeClasspath
            options.addBooleanOption('html5', true)
        }


        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact tasks.packageJavadocs
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }


        tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }
        pmd {
            toolVersion = '6.30.0'
            ruleSets = [
                    "${project.rootDir}/check/pmd/rules/errorprone.xml",
                    "category/java/multithreading.xml",
                    "category/java/bestpractices.xml",
                    "category/java/performance.xml"
            ]

        }

        test {
            useJUnitPlatform()
        }
        spotless {
            java {
                googleJavaFormat('1.9')
            }
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            testAnnotationProcessor 'org.projectlombok:lombok'
        }
    }

}
build.dependsOn assemble

defaultTasks(
        'clean',
        'assemble',
        'build',
        'spotlessApply',
        'javadoc',
        'publishToMavenLocal'
)


